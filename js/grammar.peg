{
  var CURR_ID = 0;
  var SYMBOL_TABLE = {
    "Alpha":"913",
    "Beta":"914",
    "Gamma": "915",
    "alpha": "945",
    "beta":"946",
    "gamma": "947",
  };
  var STATS = {};
  var FORMULA = new Formula();
  var cleanup = function(){
    FORMULA.cleanup_and_reassign();
  }
  var append_op = function(p, e){
    e.set_parent(p.id,true);
    return p;
  }
  var create_op = function(e,m){
    if(!(m in STATS))
        STATS[m] = 0;
    STATS[m]+=1;
    var p = FORMULA.add(m);
    for(var i=0; i < e.length; i++){
      e[i].set_parent(p.id);
    }
    return p;
  }
  var create_term = function(dict,m){
    var node = FORMULA.add(m);
    if(!(m in STATS))
      STATS[m] = 0;
    STATS[m]+=1;
    for(var d in dict){
      node.set(d, dict[d]);
    }
    return node;
  }
  var print_stats = function(){
    var total = 0;
    for( k in STATS){
      console.log(k, ":", STATS[k],'\n');
      total += STATS[k];
    }
    console.log("--------\n");
    console.log("TOTAL:", total,"\n");
    console.log("--------\n");
  }
  var handle_regex = function(first, rest){
    var handle = [first];
    for(var i=0; i < rest.length; i++){
      handle.push(rest[i][1]);
    }
    return handle;
  }

}

start =
  e:exp
  {
    FORMULA.set_root(e.id);
    cleanup();
    print_stats();
    return FORMULA;
  }

//expanded for performance purposes

exp "exp" =
  t:PEDMASEQ {return t;}


P "P" =
  t:paren {return t;}
  /t:value {return t;}

PE "PE" = 
  t:expo {return t;}
  /t:paren {return t;}
  /t:value {return t;}


PED "PED" = 
  t:div {return t;}
  /t:expo {return t;}
  /t:paren {return t;}
  /t:value {return t;}

PEDM "PEDM" = 
  t:mult {return t;}
  /t:div {return t;}
  /t:expo {return t;}
  /t:paren {return t;}
  /t:value {return t;}

PEDMA "PEDMA" = 
  t:plus {return t;}
  /t:mult {return t;}
  /t:div {return t;}
  /t:expo {return t;}
  /t:paren {return t;}
  /t:value {return t;}

PEDMAS "PEDMAS" = 
  t:sub {return t;}
  /t:plus {return t;}
  /t:mult {return t;}
  /t:div {return t;}
  /t:expo {return t;}
  /t:paren {return t;}
  /t:value {return t;}
  
  
  

PEDMASEQ "PEDMASEQ" = 
  t:eq {return t;}
  /t:sub {return t;}
  /t:plus {return t;}
  /t:mult {return t;}
  /t:div {return t;}
  /t:expo {return t;}
  /t:paren {return t;}
  /t:value {return t;}

number "number" =
   v:DECIMAL { return create_term({code:(""+v), value:v}, "NUMBER"); }

variable "variable" =
   v:LITERAL { return create_term({code:(""+v.sym), name:v.name}, "VAR"); }

value "value" =
   v:variable {return v;}
   / n:number {return n;}

paren "paren" =
    OPAREN e:PEDMAS CPAREN  { return create_op([e], "PAREN"); }

expo "expo" =
    e1:P e2:(EXP P)+ { return create_op(handle_regex(e1,e2), 'EXPO'); }


div "div" =
    e1:PE e2:(DIV PE)+ { return create_op(handle_regex(e1,e2), 'DIV'); }

mult "mult" =
    e1:PED e2:(MULT PED)+ { return create_op(handle_regex(e1,e2), 'MULT'); }

plus "plus" =
    e1:PEDM e2:(PLUS PEDM)+ { return create_op(handle_regex(e1,e2), 'ADD'); }

sub "sub" =
    e1:PEDMA e2:(MINUS PEDMA)+ { return create_op(handle_regex(e1,e2), 'SUB'); }

eq "eq" =
    e1:PEDMAS e2:(EQUALS PEDMAS)+ { return create_op(handle_regex(e1,e2), 'EQ'); }
/*

Basic Symbols

*/

DECIMAL "DECIMAL" =
   mant:[0-9]+"."dec:[0-9]+ SEP* {
      var v= mant.join("")+"."+dec.join("");
      return parseFloat(v,10)
    }
  / digits:[0-9]+ SEP* {return parseInt(digits.join(""),10)}

LITERAL "LITERAL" =
   l:[A-Za-z]+ SEP*
    {
      var name = l.join("");
      var sym = name;
      return {name:name, sym:sym}
    }
  / "\\"l:[A-Za-z]+
    {
      var name = l.join("");
      var sym = "@"+SYMBOL_TABLE[name];
      return {name:name, sym:sym}
    }


SEP "SEP"
  = [ \t\r\n]

MULT "MULT"
= "\\cdot"SEP* {return "&#8727;"}
/ "\\times"SEP* {return "x"}
/ "*"SEP* {return "*"}

DIV "DIV"
= "\\frac"SEP* {return ""}
/ "/" {return ""}

PLUS "PLUS"
= "\\plus"SEP* {return "+"}
/ "+"SEP* {return "+"}

MINUS "MINUS"
= "-"SEP* {return "-"}

EXP "EXP"
= "^"SEP* {return ""}

EQUALS "EQUALS"
= "="SEP* {return "="}


OCBRACE "OCBRACE"
  = "{"SEP*  {}

CCBRACE "CCBRACE"
  = "}"SEP* {}

OPAREN "OPAREN"
  = "("SEP* {}

CPAREN "CPAREN"
  = ")"SEP* {}
